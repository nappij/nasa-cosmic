C
      SUBROUTINE RINCIN(IW,ICLS,LINE,IPIX,NUM,NREC,NEL,NCLS,NREG,
     .   NDEVM,NDEVO,IDEV,IX)
C
C     THIS SUBROUTINE CONVERTS REGION IDENTIFICATION NUMBERS (RIN'S)
C     TO CLASS IDENTIFICATION NUMBERS (CIN'S)
C     DEFINE FILE IDEV (NREC, NEL, L, IV) REQUIRED IN CALLING PROGRAM
C
      DIMENSION LINE(NCLS),IPIX(NCLS),NUM(NCLS)
      INTEGER*2 IW(NEL),ICLS(NREG)
      LOGICAL*1 IX(NEL)
C
C     PUT IMAGE ON DISK
      DO 10 I=1,NREC
      READ(NDEVM) IW
      WRITE(IDEV'I) IW
   10 CONTINUE
C
C     ZERO THE LOOKUP TABLE
      DO 15 I=1,NREG
   15 ICLS(I) = 0
C
C     READ COORDINATE VALUES AND CORRESPONDING CLASS NUMBERS
      READ (5,100) (LINE(I), IPIX(I), NUM(I), I=1,NCLS)
      WRITE (6,200) (LINE(I), IPIX(I), NUM(I), I=1,NCLS)
C
C     STORE CLASS NUMBERS IN LOOKUP TABLE
      DO 30 I=1,NCLS
      READ(IDEV'LINE(I)) IW
      IREG=IW(IPIX(I))
      ICLS(IREG)=NUM(I)
   30 CONTINUE
C
C     REPLACE REGION NUMBERS WITH CLASS NUMBERS
      REWIND NDEVM
      DO 60 I=1,NREC
      READ(NDEVM) IW
      DO 50 J=1,NEL
      IF(IW(J).EQ.0) GO TO 46
      IF(IW(J).EQ.1) GO TO 45
      IW(J)=ICLS(IW(J))
      GO TO 46
   45 IW(J)=0
   46 IX(J)=IW(J)
   50 CONTINUE
      WRITE(NDEVO) IX
   60 CONTINUE
      RETURN
C
  100 FORMAT(5(3I5))
  200 FORMAT(5(10X,3I5))
      END
