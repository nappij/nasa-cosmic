C
      SUBROUTINE DBOUND(NREC,NEL,NEL2,NTAPI,NTAPO,IX,IY)
C
C  THE PURPOSE OF THIS PROGRAM IS TO MODIFY POINTS IN THE IMAGE ON NTAPI
C  WITH NUMBER 0 TO THE NUMBER OCCURRING MOST FREQUENTLY IN A 3 BY 3
C  NEIGHBORHOOD
C
C     ******************************************************************
C
      LOGICAL*1 IX(NEL2,3), IY(NEL)
C
C     INITIALIZE ARRAY IX.
      I1=1
      I2=2
      I3=3
      CALL SARN(NTAPI,IX(2,I1),NEL*2)
      IX(1,I1)=IX(2,I1)
      IX(NEL2,I1)=IX(NEL+1,I1)
      DO 5 J=1,NEL2
    5 IX(J,I2) = IX(J,I1)
C
C     IF(I.LT.NREC) READ (I+1)'ST RECORD INTO IX(*,I3).
C     FOR LAST RECORD, MOVE IX(*,I2) INTO IX(*,I3).
      DO 10 I=1,NREC
      IF (I.LT.NREC) GO TO 8
      DO 6 J=1,NEL2
    6 IX(J,I3) = IX(J,I2)
      GO TO 9
    8 CALL SARN (NTAPI,IX(2,I3),NEL*2)
      IX(1,I3)=IX(2,I3)
      IX(NEL2,I3)=IX(NEL+1,I3)
    9 CONTINUE
C
C     NOW, THE PREVIOUS, CURRENT AND NEXT ROWS ARE IN IX(*,I1),IX(*,I2)
C     AND IX(*,I3) RESPECTIVELY.  MODIFY EACH 0 IN IX(*,I2) TO THE MAJO-
C     RITY CLASS NUMBER IN THE 3 BY 3 NEIGHBORHOOD OF IT.
      DO 20 J=1,NEL
      IY(J)=IX(J+1,I2)
   20 IF(IY(J).EQ.0)IY(J)=MAJOR(IX,NEL2,I1,I2,I3,J+1)
      WRITE(NTAPO) IY
C
C     MODIFY I1,I2,I3 IN PREPARATION FOR THE NEXT RECORD.
      IW=I1
      I1=I2
      I2=I3
   10 I3=IW
      RETURN
      END
C
      FUNCTION MAJOR(IX,NEL,I1,I2,I3,J)
C
C     FIND THE MOST FREQUENTLY OCCURRING NUMBER AMONG THE EIGHT
C     NEIGHBORS OF IX(J,I2).  NOTE THAT 1.LT.J.LT.NEL.
C
C***********************************************************************
C
      LOGICAL*1 IX(NEL,3)
      DIMENSION LABEL(8),NUMBER(8)
C
      LABEL(1)=IX(J-1,I1)
      NUMBER(1)=1
      N=1
      J2=J-2
C
      DO 30 I=1,3
      IF(I.EQ.1)II=I1
      IF(I.EQ.2)II=I2
      IF(I.EQ.3)II=I3
      KM=1
      IF(I.EQ.1)KM=2
      INC=1
      IF(I.EQ.2)INC=2
C
      DO 10 K=KM,3,INC
      DO 20 L=1,N
      IF(IX(J2+K,II).EQ.LABEL(L))GO TO 40
20    CONTINUE
      N=N+1
      LABEL(N)=IX(J2+K,II)
      NUMBER(N)=1
      GO TO 10
40    NUMBER(L)=NUMBER(L)+1
10    CONTINUE
30    CONTINUE
C
      DO 60 I=1,N
      IF(LABEL(I).EQ.0) NUMBER(I)=0
60    CONTINUE
C
      MAX=0
      DO 50 I=1,N
      IF(NUMBER(I).LE.MAX)GO TO 50
      MAJOR=LABEL(I)
      MAX=NUMBER(I)
50    CONTINUE
      RETURN
      END
C
      SUBROUTINE DARN(IDEV,IREC,X,N)
C
C     THIS SUBROUTINE READS N BYTES FROM DIRECT ACCESS DEVICE IDEV
C     STARTING AT RECORD IX INTO ARRAY X
C
      LOGICAL*1 X(N)
      READ(IDEV'IREC)X
      RETURN
C
      ENTRY DAWN(IDEV,IREC,X,N)
C
C  THIS ENTRY WRITES N BYTES OF ARRAY X ONTO RECORD IX OF DIRECT ACCESS
C  DEVICE IDEV
      WRITE(IDEV'IREC)X
      RETURN
C
      ENTRY SARN(NTAPI,X,N)
C
C  THIS ENTRY READS N BYTES FROM SEQUENTIAL ACCESS UNIT NTAPI INTO
C  ARRAY X
      READ(NTAPI)X
      RETURN
C
      ENTRY SAWN(NTAPO,X,N)
C
C  THIS ENTRY WRITES N BYTES OF ARRAY X ONTO SEQUENTIAL ACCESS
C  UNIT NTAPI
      WRITE(NTAPO)X
      RETURN
      END
