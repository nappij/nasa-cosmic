C
      SUBROUTINE VREDUC (KNT,KOUNT,INT,NUM,N,NPOP,IFEAT,JFAC,IMOD,JMOD)
C
C     KNT=A VARIABLE USED TO COUNT THE NUMBER OF PICTURE ELEMENTS(PELS)
C     KOUNT=A VARIABLE USED TO COUNT THE NUMBER OF DIFFERENT VECTORS
C     INT = INTEGER MULTIPLIER TO WHICH COMPONENTS ARE MODIFIED
C     NUM = MAXIMUM POPULATION FOR WHICH VECTORS ARE MODIFIED
C     N(I)=COMPONENTS OF THE I'TH VECTOR IN THE TABLE
C     NPOP(I)=THE NUMBER OF OCCURRENCES OF THE ITH VECTOR IN THE TABLE
C     IFEAT=MAXIMUM NUMBER OF DIFFERENT VECTORS ALLOWED
C     JFAC=MULTIPLIER
C     IMOD=DIVISOR
C     JMOD=BASE
C
      DIMENSION N(IFEAT), NPOP(IFEAT), NN(300), MSE(4)
      LOGICAL*1 LM(4), L0(4)
      EQUIVALENCE (IM,LM(1)), (NOLD,L0(1))
      DATA NBAND /4/
C
      KONST = INT/2
      DO 400 NB=1,NBAND
  400 MSE(NB) = 0
C
      DO 445 I=1,IFEAT
      IF (NPOP(I).EQ.0) GO TO 445
      IF (NPOP(I).GT.NUM) GO TO 445
C
C     COMPUTE THE COMPONENTS OF THE INTEGER MODE VECTOR
      IM = N(I)
      NOLD = IM
      DO 410 NC=1,4
  410 LM(NC) = (LM(NC)+KONST)/INT*INT
C
C     IF THE VECTOR IS UNCHANGED, JUMP OUT
      IF (IM.EQ.NOLD) GO TO 445
C
C     HASH ROUTINE
C     COMPUTE MEAN SQUARED ERROR
      L=0
      DO 415 NC=1,4
      NX1 = L0(NC)
      NXX = LM(NC)
      MSE(NC) = MSE(NC) + NPOP(I)*IABS(NX1-NXX)
      L = JMOD*L + MOD(NXX,IMOD)
  415 CONTINUE
      L=L*JFAC+1
C
C     CHECK FOR EMPTY TABLE LOCATION TO PUT NEW VECTOR
  417 IF (NPOP(L).GT. 0) GO TO 425
      IF (NPOP(L).EQ.-1) GO TO 435
C
C     HAVE FOUND A NEW VECTOR
C     TRANSFER POPULATION AND NEW VECTOR TO TABLE LOCATION L
C     PUT POINTER FLAG AND POINTER AT OLD TABLE LOCATION
      NPOP(L) = NPOP(I)
      N(L) = IM
      NPOP(I) = -1
      N(I) = L
      GO TO 445
C
C     TABLE LOCATION IS FILLED,
C     CHECK TO SEE IF VECTOR IS IN TABLE
  425 IF (N(L).NE.IM) GO TO 435
C
C     INTEGER MODE CREATED A PREVIOUSLY EXISTING VECTOR
C     ADD PREVIOUS POPULATION AND REDUCE VECTOR COUNT BY 1
C     PUT POINTER FLAG AND POINTER AT OLD TABLE LOCATION
      NPOP(L) = NPOP(L) + NPOP(I)
      KOUNT=KOUNT-1
      NPOP(I) = -1
      N(I) = L
      GO TO 445
C
C     VECTOR NOT IN TABLE, INCREMENT TABLE INDEX, GO BACK TO CHECK
C     ON TABLE LOCATION
  435 L=L+1
      IF(L.GT.IFEAT) L=1
      GO TO 417
  445 CONTINUE
C
      WRITE (6,750)
      WRITE(6,860) INT,NUM
      WRITE (6,800) KOUNT
C
C     WRITE OUT FEATURE VECTORS THAT OCCUR AT LEAST 1000 TIMES
      NVEC = 0
      DO 450 I=1,IFEAT
      IF (NPOP(I).LT.1000)GO TO 450
      IM = N(I)
      NVEC = NVEC + 1
      IF (NVEC.EQ.1) WRITE (6,810)
      IF (MOD(NVEC,2).EQ.1) WRITE (6,811) LM, NPOP(I)
      IF (MOD(NVEC,2).EQ.0) WRITE (6,812) LM, NPOP(I)
  450 CONTINUE
C
C     POPULATION DISTRIBUTION IN LOGARITHMIC INCREMENTS
      DO 500 I=1,300
  500 NN(I)=0
      DO 550 I=1,IFEAT
      IF (NPOP(I).LT.1) GO TO 550
C
C     COUNT THE NUMBER OF VECTORS THAT OCCUR 1000'S OF TIMES
      II=NPOP(I)/1000
      IF(II.LT.1)GO TO 510
      II=II+108
      GO TO 540
C
C     COUNT THE NUMBER OF VECTORS THAT OCCUR 100'S OF TIMES
  510 II=NPOP(I)/100
      IF (II.LT.1)GO TO 530
      II=II+99
      GO TO 540
C
C     COUNT THE NUMBER OF VECTORS THAT OCCUR FROM 1 TO 99 TIMES
  530 II=NPOP(I)
  540 NN(II)=NN(II)+1
  550 CONTINUE
C
C     PRINT THE NUMBER OF VECTORS THAT OCCUR 1-99 TIMES, 100'S AND
C     1000'S OF TIMES
      WRITE (6,815)
      J = 0
      INC = 1
      DO 560 I=1,300
      IF (I.EQ.101) INC = 100
      IF (I.EQ.110) INC = 1000
      J = J + INC
      IF (NN(I).EQ.0) GO TO 560
      X = NN(I)*100.0/KOUNT
      WRITE (6,820) J, NN(I), X
  560 CONTINUE
C
C     COMPUTE THE MEAN, VARIANCE, AVERAGE MSE
      CNT = KNT
      ALN2 = ALOG10(2.0)
      AMSE = 0.0
      DO 610 NB=1,NBAND
      NXM = 0
      NXV = 0
      DO 600 I=1,IFEAT
      IF (NPOP(I).LT.1) GO TO 600
      IM = N(I)
      NXX = LM(NB)
      NXM = NXM + NPOP(I)*NXX
      NXV = NXV + NPOP(I)*NXX**2
  600 CONTINUE
      XMEAN = NXM / CNT
      SIGMA = (NXV-CNT*XMEAN**2) / (CNT-1.0)
      SQERR = MSE(NB) / CNT
      AMSE = AMSE + SQERR
  610 WRITE (6,830) NB, XMEAN, SIGMA, SQERR
      AMSE = AMSE/NBAND
      WRITE (6,840) AMSE
      RETURN
C
  750 FORMAT ('1')
  800 FORMAT (//'   VECTOR NO. =',I8)
  810 FORMAT ('1',20X,'VECTORS WITH POPULATIONS OF AT LEAST 1000'//)
  811 FORMAT (10X,4I4,I10)
  812 FORMAT ('+',50X,4I4,I10)
  815 FORMAT ('1',10X,'NO. OF TIMES'/8X,'A VECTOR OCCURRED',10X,'NO. OF
     .VECTORS',10X,'PERCENT OF TOTAL'//)
  820 FORMAT (I20,I25,F25.4)
  830 FORMAT (/10X,'BAND',I3,5X,'MEAN =',F10.3,5X,'VARIANCE =',F10.3,5X,
     .'MEAN SQUARED ERROR ='F8.4)
  840 FORMAT (/60X,'AVERAGE MEAN SQUARED ERROR =',F8.4)
  860 FORMAT ('  INT =',I5,'    NUM =',I6)
      END
