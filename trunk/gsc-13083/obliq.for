      REAL*8 FUNCTION OBLIQ(TSEC50)
      IMPLICIT REAL*8 (A-H,O-Z)
C
C  THIS ROUTINE COMPUTES THE MEAN OBLIQUITY OF THE ECLIPTIC AT A
C  GIVEN TIME. THAT IS, IT COMPUTES THE ANGLE BETWEEN THE 
C  ECLIPTIC PLANE AND THE MEAN EQUATOR OF DATE(DATE REFERS TO 
C  THE INPUT TIME).
C
C VARIABLE  DIM  TYPE I/O DESCRIPTION
C --------  ---  ---- --- -----------
C
C TSEC50     1   R*8   I  TIME AT WHICH THE MEAN OBLIQUITY IS WANTED.
C                         IN SECONDS SINCE 1/1/50,0.0 HRS, ET.
C                         ACCURACY IS NOT HIGH ENOUGH TO WARRANT DOING
C                         AN ET-UT CORRECTION. USE UT IF IT IS MORE 
C                         CONVENIENT.
C
C OBLIQ      1   R*8   O  FUNCTION VALUE RETURNED. IN RADIANS.
C
C***********************************************************************
C
C  BY C PETRUZZO. 12/82.
C  MODIFIED... 7/83. CJP. COMMENT MOD. NO CODE CHANGED.
C
C***********************************************************************
C
C  NOTE -- THE CONSTANTS AND FORMULA ARE FROM THE EXPLANATORY SUPPLEMENT
C          TO THE ASTRONOMICAL ALMANAC AND THE AMERICAN EPHEMERIS AND
C          NAUTICAL ALMANAC, 1974(=1961+AMMENDMENTS), PAGE 98. THE
C          GSFC LIBRARY NUMBER IS QB8.U5, E96.
C
      REAL*8 C0/23.452294D0/, C1/-0.0130125D0/, C2/-0.164D-5/,
     1       C3/0.0503D-5/
C
      REAL*8 HAFCENT/18262.5D0/,CENT/36525.D0/,SECDAY/86400.D0/
      REAL*8 DEGRAD/ 57.29577951308232D0 /
C
C   GET INPUT TIME AS JULIAN CENTURIES SINCE JAN 0.5,1900, E.T.
      TFRAC=(HAFCENT+TSEC50/SECDAY)/CENT
C
      T1 = TFRAC
      T2 = T1*T1
      T3 = T2*T1
      TILT = C0 + C1*T1 + C2*T2 + C3*T3
C
      OBLIQ = TILT/DEGRAD
      RETURN
      END

